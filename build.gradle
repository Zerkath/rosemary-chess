plugins {
    id 'java'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-Xlint:deprecation')
    options.compilerArgs.add('-Xlint:unchecked')
    options.compilerArgs.add('-Xlint:cast')
}

test {
    jvmArgs = ['-Xms512m', '-Xmx8G']

    beforeTest { descriptor ->
       logger.lifecycle("Running test: " + descriptor)
    }

    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter:5.7.2")
}

jacocoTestReport {
    reports {
        xml.required.set(false)
        csv.required.set(false)
        html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
    }
    dependsOn(tasks.test)
}

jar {
  manifest {
    attributes(
        "Manifest-Version": "1.0",
        "Main-Class": "com.github.zerkath.rosemary.Main.Program"
        )
  }
}
