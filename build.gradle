plugins {
  id 'java'
  id 'jacoco'
  id 'com.diffplug.spotless' version '8.0.0'
  id "me.champeau.jmh" version "0.7.3"
}

repositories {
  mavenCentral()
}

tasks.withType(JavaCompile) {
options.encoding = 'UTF-8'
  options.compilerArgs.add('-Xlint')
  // options.compilerArgs.add('-g:none')
}

test {
  jvmArgs = ['-Xms512m', '-Xmx8G']

  testLogging {
    // Show stack traces for all test events
    events 'failed'
  }

  useJUnitPlatform {
    excludeTags 'slow' // TODO: Enable the slow tests when needed
  }

  finalizedBy(tasks.jacocoTestReport)
}

dependencies {
  testImplementation("org.junit.platform:junit-platform-launcher:6.0.1")
  testImplementation("org.junit.jupiter:junit-jupiter:6.0.1")
  testImplementation("org.junit.jupiter:junit-jupiter-params:6.0.1")
  implementation "org.openjdk.jmh:jmh-core:1.37"
  implementation "org.openjdk.jmh:jmh-generator-annprocess:1.37"
  implementation 'it.unimi.dsi:fastutil:8.5.13'
}

jacocoTestReport {
  reports {
    xml.required.set(false)
    csv.required.set(true)
  }
  dependsOn(tasks.test)
}

spotless {
  // optional: limit format enforcement to just the files changed by this feature branch
  // ratchetFrom 'origin/main'

  format 'misc', {
    // define the files to apply `misc` to
    target '*.gradle', '*.md', '.gitignore'

    // define the steps to apply to those files
    trimTrailingWhitespace()
    indentWithSpaces(2) // or spaces. Takes an integer argument if you don't like 4
    endWithNewline()
  }

  java {
    // Use the default importOrder configuration
    importOrder()
    removeUnusedImports()
    // Choose one of these formatters.
    // googleJavaFormat().aosp()

    // prettier()
    // clangFormat()

    formatAnnotations()  // fixes formatting of type annotations, see below
  }
}

jar {
  manifest {
    attributes(
      "Manifest-Version": "1.0",
      "Main-Class": "rosemary.Program"
      )
  }
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  duplicatesStrategy = 'exclude' // Or 'warn', 'fail', etc. depending on your preference
}
